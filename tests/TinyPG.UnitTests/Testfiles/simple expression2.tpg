//@TinyPG - a Tiny Parser Generator v1.2
//Copyright Â© Herre Kuijpers 2008-2010
//Simple expression calculator


EOF         -> @"^\s*$";
NUMBER 	-> @"[0-9]+";
PLUSMINUS 	-> @"(\+|-)";
MULTDIV 	-> @"\*|/";
BROPEN 	->  @"\(";
BRCLOSE 	->  @"\)";

[Skip] WHITESPACE  -> @"\s+";

Start -> (AddExpr)? EOF
{ return $AddExpr; };      

AddExpr -> MultExpr (PLUSMINUS MultExpr)*
{ 
	int Value = Convert.ToInt32($MultExpr);
	int i = 1;
	while ($MultExpr[i] != null)
	{
		string sign = $PLUSMINUS[i-1].ToString();
		if (sign == "+")
			Value += Convert.ToInt32($MultExpr[i++]);
		else 
			Value -= Convert.ToInt32($MultExpr[i++]);
	}

	return Value; 
};

MultExpr -> Atom (MULTDIV Atom)*
{ 
	int Value = Convert.ToInt32($Atom);
	int i = 1;
	while ($Atom[i] != null)
	{
		string sign = $MULTDIV[i-1].ToString();
		if (sign == "*")
			Value *= Convert.ToInt32($Atom[i++]);
		else 
			Value /= Convert.ToInt32($Atom[i++]);
	}
	return Value; 
};

Atom -> NUMBER | BROPEN AddExpr BRCLOSE
{
	if ($NUMBER != null) return $NUMBER; 
	else return $AddExpr; 
};